%LET _CLIENTTASKLABEL='MAIN ANALYSES';
%LET _CLIENTPROCESSFLOWNAME='Process Flow';
%LET _CLIENTPROJECTPATH='\\pgcw01fscl03\Users$\FKU838\Documents\Projects\rxad\RXAD - Seo.egp';
%LET _CLIENTPROJECTNAME='RXAD - Seo.egp';
%LET _SASPROGRAMFILE=;

GOPTIONS ACCESSIBLE;
/* PATIENT CHARACTERISTICS ACROSS USE OF DRUGS */
PROC FREQ DATA=RXAD_STUDY; TABLE STATN BBLKR CBLKR DIURT RASIH PPINH; RUN;
PROC UNIVARIATE DATA=RXAD_STUDY; VAR AGE_AT_STRT AGE_AT_END MONTH2EV; HISTOGRAM; RUN;
PROC MEANS DATA=RXAD_STUDY; VAR MONTH2EV; OUTPUT OUT=TAB0 MEDIAN=M Q1=P1 Q3=P3 QRANGE=IQR; RUN;
%MACRO DESC(IF,RX);
DATA VNAMES1; SET VNAMES;
	IF 106 <= VARNUM <= 193;
	IF 107 <= VARNUM <= 112 OR 114 <= VARNUM <= 117 OR 119 <= VARNUM <= 122  THEN DELETE;
	FLAG  = (VARNUM IN (113,118,135));
RUN;
DATA _NULL_;
	SET VNAMES1;
	CALL SYMPUT(("COV"||COMPRESS(_N_)), NAME);
	CALL SYMPUT(('FLAG'||COMPRESS(_N_)),FLAG);
	CALL SYMPUT('N',_N_);
RUN;
DATA TEMP; SET RXAD_STUDY; &IF; RUN;
%LOCAL I;
%LET I = 1;
%DO I = 1 %TO &N;
	%IF &&FLAG&I = 1 %THEN %DO;	
	PROC MEANS DATA=TEMP;
		VAR &&COV&I; 
		OUTPUT OUT=TAB0 MEAN=M SUM = T STD=SD; 
	RUN;
	DATA TAB0; 
		FORMAT VARIABLE &RX $20.;
		SET TAB0; 
		KEEP VARIABLE &RX;
		VARIABLE = "&&COV&I";
		%IF &I ^=16 %THEN %DO; 
		&RX = TRIM(LEFT(ROUND(M,0.1)))||'('|| TRIM(LEFT(ROUND(SD,0.1)))||')'; 
		%END;
		%ELSE %DO;
		&RX = TRIM(LEFT(ROUND(M,0.1)))||'('|| TRIM(LEFT(ROUND(SD,0.1)))||';'||TRIM(LEFT(ROUND(T,0.1)))||')'; 
		%END;
		RUN;
	RUN;
	%END;

	%IF &&FLAG&I ^= 1 %THEN %DO;
	ODS OUTPUT ONEWAYFREQS=TAB0(WHERE=(&&COV&I = 1) KEEP=&&COV&I FREQUENCY PERCENT);
		PROC FREQ DATA=TEMP; 
		TABLE &&COV&I; 
	RUN;
	DATA TAB0; 
		FORMAT VARIABLE &RX $20.; 
		SET TAB0; 
		KEEP VARIABLE &RX;
		VARIABLE = "&&COV&I"; 
		&RX = STRIP(PUT(FREQUENCY,COMMA10.0))||'('|| STRIP(LEFT(ROUND(PERCENT,0.1)))||')'; 
	RUN;
	%END;
	
	%IF &I=1 %THEN %DO;
		DATA RXAD_TAB1_&RX;
			SET TAB0;
		RUN;
	%END;

	%IF &I^=1 %THEN %DO;
		DATA RXAD_TAB1_&RX;
			SET RXAD_TAB1_&RX TAB0;
		RUN;
	%END;
%END;
%MEND;
%DESC(IF =                   , RX = OVERALL);
%DESC(IF = %STR(IF STATN = 1), RX = STATN);
%DESC(IF = %STR(IF BBLKR = 1), RX = BBLKR);
%DESC(IF = %STR(IF CBLKR = 1), RX = CBLKR);
%DESC(IF = %STR(IF DIURT = 1), RX = DIURT);
%DESC(IF = %STR(IF RASIH = 1), RX = RASIH);
%DESC(IF = %STR(IF PPINH = 1), RX = PPINH);

/* SURVIVAL ANALYSIS - TIMES TO DEMENTIA */
/* COX PH REGRESSION AND COMPETING RISK REGRESSION (FINE-GREY SUBDISTRIBUTION HAZARD REGRESSION)*/
/* MODELS INCLUDE TIME-FIXED AND TIME-VARYING COVARIATES */


%MACRO REG(C,E,I);
OPTIONS CMPLIB=WORK.CMPL;
ODS OUTPUT PARAMETERESTIMATES = PRM&I;
PROC PHREG DATA=RXAD_STUDY;
	MODEL TIME2EV*STATUS(&C) = &COV &CC/RL TIES=EFRON &E;
	T = MIN(INTNX('MONTH','01JAN2007'D, STRT+TIME2EV-1),END_DT); 
	/*
	STATN_T = (SUBSTR(STATN1,STRT+TIME2EV-1,1) = '1');
	BBLKR_T = (SUBSTR(BBLKR1,STRT+TIME2EV-1,1) = '1');
	CBLKR_T = (SUBSTR(CBLKR1,STRT+TIME2EV-1,1) = '1');
	DIURT_T = (SUBSTR(DIURT1,STRT+TIME2EV-1,1) = '1');
	RASIH_T = (SUBSTR(RASIH1,STRT+TIME2EV-1,1) = '1');
	PPINH_T = (SUBSTR(PPINH1,STRT+TIME2EV-1,1) = '1');
	*/
	/*
	STATN_T = INPUT(SUBSTR(STATN2,3*(STRT+TIME2EV-1)-2,2),BEST12.)/30;
	BBLKR_T = INPUT(SUBSTR(BBLKR2,3*(STRT+TIME2EV-1)-2,2),BEST12.)/30;
	CBLKR_T = INPUT(SUBSTR(CBLKR2,3*(STRT+TIME2EV-1)-2,2),BEST12.)/30;
	DIURT_T = INPUT(SUBSTR(DIURT2,3*(STRT+TIME2EV-1)-2,2),BEST12.)/30;
	RASIH_T = INPUT(SUBSTR(RASIH2,3*(STRT+TIME2EV-1)-2,2),BEST12.)/30;
	PPINH_T = INPUT(SUBSTR(PPINH2,3*(STRT+TIME2EV-1)-2,2),BEST12.)/30;
	*/
	
	ARRAY RX1[*] S1 - S108;
	ARRAY RX2[*] B1 - B108;
	ARRAY RX3[*] C1 - C108;
	ARRAY RX4[*] D1 - D108;
	ARRAY RX5[*] R1 - R108;
	ARRAY RX6[*] P1 - P108;
		
	STATN_T = RX1[MIN(STRT+TIME2EV-1,108)]/(30*TIME2EV);
	BBLKR_T = RX2[MIN(STRT+TIME2EV-1,108)]/(30*TIME2EV);
	CBLKR_T = RX3[MIN(STRT+TIME2EV-1,108)]/(30*TIME2EV);
	DIURT_T = RX4[MIN(STRT+TIME2EV-1,108)]/(30*TIME2EV);
	RASIH_T = RX5[MIN(STRT+TIME2EV-1,108)]/(30*TIME2EV);
	PPINH_T = RX6[MIN(STRT+TIME2EV-1,108)]/(30*TIME2EV);
	
	DUALE_T = (SUBSTR(DUAL,STRT+TIME2EV-1,1) = 'Y');
	NDLIS_T = (SUBSTR(DUAL,STRT+TIME2EV-1,1) = 'N') AND (SUBSTR(CSTSHR,STRT+TIME2EV-1,1) = 'Y');
	NOCST_T = (SUBSTR(DUAL,STRT+TIME2EV-1,1) = 'N') AND (SUBSTR(CSTSHR,STRT+TIME2EV-1,1) = 'N');
	*NOCST_T = (COUNTC(SUBSTR(DUAL,3*(STRT+TIME2EV-1)-2,3),'Y') = 0) AND (COUNTC(SUBSTR(CSTSHR,3*(STRT+TIME2EV-1)-2,3),'Y') = 0);
	 	
	AMI_T          = (AMI_EVER ^=. AND AMI_EVER <= T);
	AFIB_T         = (ATRIAL_FIB_EVER ^=. AND  ATRIAL_FIB_EVER <= T);
	CATARCT_T      = (CATARACT_EVER ^=. AND CATARACT_EVER <= T);
	KIDENEY_T      = (CHRONICKIDNEY_EVER ^=. AND CHRONICKIDNEY_EVER <= T);
	COPD_T         = (COPD_EVER ^=. AND COPD_EVER <= T);
	CHF_T          = (CHF_EVER ^=. AND CHF_EVER <= T);
	DIABETS_T      = (DIABETES_EVER ^=. AND DIABETES_EVER <= T);
	GLAUCOM_T      = (GLAUCOMA_EVER ^=. AND GLAUCOMA_EVER <= T);
	HIPFRAC_T      = (HIP_FRACTURE_EVER ^=. AND HIP_FRACTURE_EVER <= T);
	ISCHMIC_T      = (ISCHEMICHEART_EVER ^=. AND ISCHEMICHEART_EVER <= T);
	DEPRESN_T      = (DEPRESSION_EVER ^=. AND DEPRESSION_EVER <= T);
	OSTEOPS_T      = (OSTEOPOROSIS_EVER ^=. AND OSTEOPOROSIS_EVER <= T);
	RA_OA_T        = (RA_OA_EVER ^=. AND RA_OA_EVER <= T);
	STROKE_T       = (STROKE_TIA_EVER ^=. AND STROKE_TIA_EVER <= T);
	CBREAST_T      = (CANCER_BREAST_EVER ^=. AND CANCER_BREAST_EVER <= T);
	CCOLREC_T      = (CANCER_COLORECTAL_EVER ^=. AND CANCER_COLORECTAL_EVER <= T);
	CPRSTAT_T      = (CANCER_PROSTATE_EVER ^=. AND CANCER_PROSTATE_EVER <= T);
	CLUNG_T        = (CANCER_LUNG_EVER ^=. AND CANCER_LUNG_EVER <= T);
	CENDMT_T       = (CANCER_ENDOMETRIAL_EVER ^=. AND CANCER_ENDOMETRIAL_EVER <= T);
	ANEMIA_T       = (ANEMIA_EVER ^=. AND ANEMIA_EVER <= T);
	ASTHMA_T       = (ASTHMA_EVER ^=. AND ASTHMA_EVER <= T);
	HYPERL_T       = (HYPERL_EVER ^=. AND HYPERL_EVER <= T);
	HYPERP_T       = (HYPERP_EVER ^=. AND HYPERP_EVER <= T);
	HYPERT_T       = (HYPERT_EVER ^=. AND HYPERT_EVER <= T);
	HYPOTH_T       = (HYPOTH_EVER ^=. AND HYPOTH_EVER <= T);
	ACPHS_T        = (ACP_MEDICARE_EVER ^=. AND ACP_MEDICARE_EVER <= T);
	ANXID_T        = (ANXI_MEDICARE_EVER ^=. AND ANXI_MEDICARE_EVER <= T);
	AUTISM_T       = (AUTISM_MEDICARE_EVER ^=. AND AUTISM_MEDICARE_EVER <= T);
	BIPLR_T        = (BIPL_MEDICARE_EVER ^=. AND BIPL_MEDICARE_EVER <= T);
	BRAINIJ_T      = (BRAINJ_MEDICARE_EVER ^=. AND BRAINJ_MEDICARE_EVER <= T);
	DEPSSN_T       = (DEPSN_MEDICARE_EVER ^=. AND DEPSN_MEDICARE_EVER <= T);
	INTDIS_T       = (INTDIS_MEDICARE_EVER ^=. AND INTDIS_MEDICARE_EVER <= T);
	LEADIS_T       = (LEADIS_MEDICARE_EVER ^=. AND LEADIS_MEDICARE_EVER <= T);
	OTHDEL_T       = (OTHDEL_MEDICARE_EVER ^=. AND OTHDEL_MEDICARE_EVER <= T);
	PSDSOD_T       = (PSDS_MEDICARE_EVER ^=. AND PSDS_MEDICARE_EVER <= T);
	SCHIZP_T       = (SCHI_MEDICARE_EVER ^=. AND SCHI_MEDICARE_EVER <= T);
	SCHZOT_T       = (SCHIOT_MEDICARE_EVER ^=. AND SCHIOT_MEDICARE_EVER <= T);
	PTRASD_T       = (PTRA_MEDICARE_EVER ^=. AND PTRA_MEDICARE_EVER <= T);
	CERPAL_T       = (CERPAL_MEDICARE_EVER ^=. AND CERPAL_MEDICARE_EVER <= T);
	EPILEP_T       = (EPILEP_MEDICARE_EVER ^=. AND EPILEP_MEDICARE_EVER <= T);
	CYSFIB_T       = (CYSFIB_MEDICARE_EVER ^=. AND CYSFIB_MEDICARE_EVER <= T);
	FIBROM_T       = (FIBRO_MEDICARE_EVER ^= . AND FIBRO_MEDICARE_EVER <= T);
	HEPVRL_T       = (HEPVIRAL_MEDICARE_EVER ^=. AND HEPVIRAL_MEDICARE_EVER <= T);
	LIVERD_T       = (LIVER_MEDICARE_EVER ^=. AND LIVER_MEDICARE_EVER <= T);
	HIVAIDS_T      = (HIVAIDS_MEDICARE_EVER ^=. AND HIVAIDS_MEDICARE_EVER <= T);
	LEUKLP_T       = (LEUKLYMPH_MEDICARE_EVER ^=. AND LEUKLYMPH_MEDICARE_EVER <= T);
	MIGRAN_T       = (MIGRAINE_MEDICARE_EVER ^=. AND MIGRAINE_MEDICARE_EVER <= T);
	MOBIMP_T       = (MOBIMP_MEDICARE_EVER ^=. AND MOBIMP_MEDICARE_EVER <= T);
	MULSCL_T       = (MULSCL_MEDICARE_EVER ^=. AND MULSCL_MEDICARE_EVER <= T);
	MUSDYS_T       = (MUSDYS_MEDICARE_EVER ^=. AND MUSDYS_MEDICARE_EVER <= T);
	OBESITY_T      = (OBESITY_MEDICARE_EVER ^=. AND OBESITY_MEDICARE_EVER <= T);
	PVASCD_T       = (PVD_MEDICARE_EVER ^=. AND PVD_MEDICARE_EVER <= T);
	SPIBIF_T       = (SPIBIF_MEDICARE_EVER ^=. AND SPIBIF_MEDICARE_EVER <= T);
	SPIINJ_T       = (SPIINJ_MEDICARE_EVER ^=. AND SPIINJ_MEDICARE_EVER <= T);
	TOBACO_T       = (TOBA_MEDICARE_EVER ^=. AND TOBA_MEDICARE_EVER <= T);
	ULCERS_T       = (ULCERS_MEDICARE_EVER ^=. AND ULCERS_MEDICARE_EVER <= T);
	HEARIM_T       = (HEARIM_MEDICARE_EVER ^=. AND HEARIM_MEDICARE_EVER <= T);
	VISUAL_T       = (VISUAL_MEDICARE_EVER ^=. AND VISUAL_MEDICARE_EVER <= T);
RUN;
DATA PRM&I; 
	FORMAT VARIABLE $20. WALD BEST12. RATIO $20.; 
	SET PRM&I; 
	KEEP VARIABLE WALD HAZARDRATIO HRLOWERCL HRUPPERCL RATIO;
	VARIABLE = PARAMETER;
	IF PRXMATCH("m/_T/oi",PARAMETER) > 0 THEN VARIABLE = REVERSE(SUBSTR(STRIP(REVERSE(PARAMETER)),3,LENGTH(PARAMETER)-2));
	WALD = CHISQ;
	RATIO = TRIM(LEFT(ROUND(HazardRatio,0.01)))||'('||TRIM(LEFT(ROUND(HRLowerCL,0.01)))||','||TRIM(LEFT(ROUND(HRUpperCL,0.01)))||')'; 
RUN;
%MEND;
%LET CC = AMI_T AFIB_T CATARCT_T KIDENEY_T COPD_T CHF_T DIABETS_T GLAUCOM_T HIPFRAC_T ISCHMIC_T DEPRESN_T OSTEOPS_T 
          RA_OA_T STROKE_T CBREAST_T CCOLREC_T CPRSTAT_T CLUNG_T CENDMT_T ANEMIA_T ASTHMA_T HYPERL_T HYPERP_T HYPERT_T  
		  HYPOTH_T ACPHS_T ANXID_T AUTISM_T BIPLR_T BRAINIJ_T DEPSSN_T INTDIS_T LEADIS_T OTHDEL_T PSDSOD_T SCHZOT_T 
		  PTRASD_T CERPAL_T EPILEP_T CYSFIB_T FIBROM_T HEPVRL_T LIVERD_T HIVAIDS_T LEUKLP_T MIGRAN_T MOBIMP_T  
		  MULSCL_T MUSDYS_T OBESITY_T PVASCD_T SPIBIF_T SPIINJ_T TOBACO_T ULCERS_T HEARIM_T VISUAL_T;
*%LET CC = ;
%LET COV = STATN_T BBLKR_T CBLKR_T DIURT_T RASIH_T PPINH_T FEMALE BLACK HISPN ASIAN OTHER DUALE_T NDLIS_T RURAL;
*%REG(C = %STR(0,2), E =                    , I = 1);
*%REG(C = %STR(0,1), E =                    , I = 2);
%REG(C = %STR(0)  , E = %STR(EVENTCODE = 1), I = 3);
%REG(C = %STR(0)  , E = %STR(EVENTCODE = 2), I = 4);




GOPTIONS NOACCESSIBLE;
%LET _CLIENTTASKLABEL=;
%LET _CLIENTPROCESSFLOWNAME=;
%LET _CLIENTPROJECTPATH=;
%LET _CLIENTPROJECTNAME=;
%LET _SASPROGRAMFILE=;

